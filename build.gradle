plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.0'
  id 'org.springframework.boot' version '2.7.8'
  id 'org.owasp.dependencycheck' version '8.1.0'
  id 'com.github.ben-manes.versions' version '0.45.0'
  id 'org.sonarqube' version '3.5.0.2730'
  id 'com.github.spacialcircumstances.gradle-cucumber-reporting' version '0.1.23'
  id 'uk.gov.hmcts.java' version '0.12.28'
  id 'com.github.hmcts.rse-cft-lib' version '0.15.64'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntimeOnly.extendsFrom runtimeOnly

  cucumberRuntime {
    extendsFrom testImplementation
  }

  cftlibRuntimeClasspath.extendsFrom(runtimeClasspath)
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  generateCucumberReports.enabled = false
  failFast = true
}

task functional() {
  description = "Runs functional tests"
  dependsOn assemble, testClasses
  doFirst {
    generateCucumberReports.enabled = true
    javaexec {
      main = "io.cucumber.core.cli.Main"
      classpath += configurations.cucumberRuntime + sourceSets.functionalTest.output
      args = [
        '--threads', '1',
        '--plugin', "json:${rootDir}/target/cucumber.json",
        '--plugin', "junit:${buildDir}/test-results/functional/cucumber.xml",
        '--tags', '',
        '--glue', 'uk.gov.hmcts.reform.hmc.jp', 'src/functionalTest/resources/features'
      ]
    }
  }
  finalizedBy {
    generateCucumberReports {
      doLast{
        delete "${rootDir}/Report for Functional Tests/"
        new File("${rootDir}/Report for Functional Tests").mkdirs()
        file("${rootDir}/target/cucumber-html-reports").renameTo(file("${rootDir}/Report for Functional Tests"))
        logger.quiet("Functional test report moved to ---> file://${buildDir}/reports/tests/functional/overview-features.html")

      }
    }
  }
}

cucumberReports {
  outputDir = file("${rootDir}/target")
  reports = files("${rootDir}/target/cucumber.json")
  notFailingStatuses = ["skipped", "passed"]
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

checkstyle {
  maxWarnings = 0
  toolVersion = '10.7.0'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.54.0"
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonar'].dependsOn jacocoTestReport
project.tasks['check'].dependsOn integration

sonar {
  properties {
    property "sonar.projectName", "Reform :: hmc-judicial-payment-service"
    property "sonar.projectKey", "uk.gov.hmcts.reform:hmc-judicial-payment-service"
    property "sonar.coverage.jacoco.xmlReportPaths", "${rootDir}/build/reports/jacoco/codeCoverageReport/codeCoverageReport.xml"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

ext {
  log4JVersion = "2.19.0"
  testcontainersVersion = '1.17.6'
}

ext['snakeyaml.version'] = '1.33'

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.14'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.0.1'

  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion

  implementation group: 'io.rest-assured', name: 'rest-assured'

  annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: '1.4.2.Final'

  testImplementation(platform('org.junit:junit-bom:5.9.2'))
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }


  implementation group: 'org.flywaydb', name: 'flyway-core', version: '9.15.2'
  compileOnly group: 'org.mapstruct', name: 'mapstruct', version: versions.mapstruct

  runtime group: 'org.postgresql', name: 'postgresql', version: '42.5.1'


  implementation group: 'io.cucumber', name: 'cucumber-java', version: '7.11.1'
  testImplementation group: 'io.cucumber', name: 'cucumber-junit', version: '7.11.1'
  testImplementation group: 'junit', name: 'junit', version: '4.13.2'

  implementation (group: 'org.glassfish.web', name: 'el-impl', version: '2.2.1-b05') {
    exclude group: 'javax.el', module: 'el-api'
  }
  implementation group: 'org.glassfish', name: 'jakarta.el', version: '4.0.2'
  implementation group: 'javax.el', name: 'javax.el-api', version: '3.0.1-b06'
  implementation group: 'org.testcontainers', name: 'junit-jupiter', version: testcontainersVersion

  cftlibImplementation group: 'org.springframework.boot', name: 'spring-boot-devtools'

}

rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'

}

mainClassName = 'uk.gov.hmcts.reform.hmc.jp.Application'

bootJar {
  archiveFileName = "hmc-judicial-payment-service.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

bootWithCCD {
  doFirst {
    // Set this flag to 'true', if you want the CFTLib containers to be recreates each time
    environment "RSE_LIB_CLEAN_BOOT", false
  }

  authMode = uk.gov.hmcts.rse.AuthMode.Local
  // TODO: update when we have db connections and authorised services
  environment("DATA_STORE_S2S_AUTHORISED_SERVICES", "ccd_gw,ccd_gateway,ccd_data,ccd_next_hearing_date_updater")
  environment("RSE_LIB_S2S_PORT", 4502)
  environment("RSE_LIB_ADDITIONAL_DATABASES", "hmc");
  environment("CFT_HEARING_SERVICE_DB_USERNAME", "postgres")
  environment("CFT_HEARING_SERVICE_DB_PASSWORD", "postgres")
  environment("CFT_HEARING_SERVICE_DB_PORT", "6432")
  environment("CFT_HEARING_SERVICE_DB_NAME", "hmc")
  environment("S2S_URL", "http://localhost:8489")
}

void loadEnvFile(Task execTask, String envFilePath) {
  if (file(envFilePath).exists()) {
    logger.quiet("Loading environment variables from '${envFilePath}'")

    file(envFilePath).readLines().each() {
      if (it.startsWith('# ')) {
        if (it.startsWith('# WARNING')) {
          // copy warning to console
          logger.quiet(it)
        }
      } else if (!it.isEmpty()) {
        def (key, value) = it.tokenize('=')
        execTask.environment(key, value)
      }
    }
  } else {
    logger.quiet("Skipping load of environment variables from '${envFilePath}' as file not found.")
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
