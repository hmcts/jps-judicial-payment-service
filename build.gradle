import org.flywaydb.gradle.task.FlywayMigrateTask

plugins {
  id 'application'
  id 'checkstyle'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.0'
  id 'org.springframework.boot' version '2.7.11'
  id 'org.owasp.dependencycheck' version '8.1.0'
  id 'com.github.ben-manes.versions' version '0.45.0'
  id 'org.sonarqube' version '4.0.0.2929'
  id 'com.github.spacialcircumstances.gradle-cucumber-reporting' version '0.1.23'
  id 'uk.gov.hmcts.java' version '0.12.43'
  id 'com.github.hmcts.rse-cft-lib' version '0.15.64'
  id 'org.flywaydb.flyway' version '9.10.1'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

def versions = [
  junit           : '5.7.2',
  junitPlatform   : '1.7.2',
  reformLogging   : '6.0.1',
  restAssured     : '4.2.1',
  springBoot      : springBoot.class.package.implementationVersion,
  springCloud     : '2020.0.1',
  springfoxSwagger: '3.0.0',
  testcontainers  : '1.15.2',
  netty           : '4.1.68.Final',
  serviceAuthVersion: '3.1.1',
  tomcatEmbedded  : '9.0.69',
  mapstruct       : '1.5.3.Final',
  lombok          : '1.18.24',
  flyway          : '9.15.2',
  postgresql      : '42.6.0'
]

ext['log4j2.version'] = '2.17.1'
ext['snakeyaml.version'] = '1.33'
ext['jackson.version'] = '2.14.1'


java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntimeOnly.extendsFrom runtimeOnly

  cucumberRuntime {
    extendsFrom testImplementation
  }

  cftlibRuntimeClasspath.extendsFrom(runtimeClasspath)
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  useJUnitPlatform()
  generateCucumberReports.enabled = false
  failFast = true
}

task smoke(type: Test) {
  description = "Runs smoke tests"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath

  dependsOn assemble, testClasses
  doFirst {
    generateCucumberReports.enabled = true
    javaexec {
      main = "io.cucumber.core.cli.Main"
      classpath += configurations.cucumberRuntime + sourceSets.functionalTest.output
      args = [
        '--threads', '1',
        '--plugin', "json:${rootDir}/target/cucumber.json",
        '--plugin', "junit:${buildDir}/test-results/smoke/cucumber.xml",
        '--tags', '@Smoke',
        '--glue', 'uk.gov.hmcts.reform.jps', 'src/functionalTest/resources/features'
      ]
    }
  }
  finalizedBy {
    generateCucumberReports {
      doLast{
        delete "${rootDir}/Report for Smoke Tests/"
        new File("${rootDir}/Report for Smoke Tests").mkdirs()
        file("${rootDir}/target/cucumber-html-reports").renameTo(file("${rootDir}/Report for Smoke Tests"))
        logger.quiet("Smoke test report moved to ---> file://${buildDir}/Report for Smoke Tests/overview-features.html")

      }
    }
  }
}

task functional(type: Test) {
  description = "Runs functional tests"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath

  dependsOn assemble, testClasses

  //To force task to run every time
  inputs.property("timestamp", System.currentTimeMillis())

  doFirst {
    generateCucumberReports.enabled = true
    javaexec {
      main = "io.cucumber.core.cli.Main"
      classpath += configurations.cucumberRuntime + sourceSets.functionalTest.output
      args = [
        '--threads', '1',
        '--plugin', "json:${rootDir}/target/cucumber.json",
        '--plugin', "junit:${buildDir}/test-results/functional/cucumber.xml",
        '--tags', '@Functional and not @Ignore',
        '--glue', 'uk.gov.hmcts.reform.jps', 'src/functionalTest/resources/features'
      ]
    }
  }
  finalizedBy {
    generateCucumberReports {
      doLast{
        delete "${rootDir}/Report for Functional Tests/"
        new File("${rootDir}/Report for Functional Tests").mkdirs()
        file("${rootDir}/target/cucumber-html-reports").renameTo(file("${rootDir}/Report for Functional Tests"))
        logger.quiet("Functional test report moved to ---> file://${buildDir}/Report for Functional Tests/overview-features.html")
      }
    }
  }
}

cucumberReports {
  outputDir = file("${rootDir}/target")
  reports = files("${rootDir}/target/cucumber.json")
  notFailingStatuses = ["skipped", "passed"]
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

checkstyle {
  maxWarnings = 0
  toolVersion = '10.7.0'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport
project.tasks['check'].dependsOn integration


def coverageExclusionList = [
  '**uk/gov/hmcts/reform/jps/exceptions/*.*',
  '**uk/gov/hmcts/reform/jps/config/**/*',
  '**uk/gov/hmcts/reform/jps/domain/*.*',
  "src/main/java/uk/gov/hmcts/reform/jps/Application.java",
  "**/config/SecurityConfiguration.java",
  "**/controllers/TestController.java",
  "**uk/gov/hmcts/reform/jps/model/**/*",
  "**uk/gov/hmcts/reform/jps/refdata/**/*",
  "**uk/gov/hmcts/reform/jps/validator/*.*",
  "**uk/gov/hmcts/reform/jps/ResponseExceptionHandler.java"
]

sonar {
  properties {
    property "sonar.projectName", "Reform :: jps-judicial-payment-service"
    property "sonar.projectKey", "uk.gov.hmcts.reform:jps-judicial-payment-service"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
    property "sonar.exclusions", coverageExclusionList.join(", ")
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

ext {
  log4JVersion = "2.19.0"
  testcontainersVersion = '1.17.6'
}

ext['snakeyaml.version'] = '1.33'

dependencies {
  annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: '1.4.2.Final'
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-client'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-resource-server'
  implementation group: 'org.springframework.security', name: 'spring-security-web'
  implementation group: 'org.springframework.security', name:'spring-security-core'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose'
  implementation group: 'org.springframework.security', name: 'spring-security-config'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.7.0', {
    exclude group: 'org.yaml', module: 'snakeyaml'
  }

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.0.1'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-api'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j'
  implementation group: 'javax.inject', name: 'javax.inject', version: '1'
  implementation group: 'io.jsonwebtoken', name: 'jjwt', version:'0.9.1'

  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion
  implementation group: 'io.rest-assured', name: 'rest-assured'

  implementation group: 'org.flywaydb', name: 'flyway-core',  version: versions.flyway
  implementation group: 'org.mapstruct', name: 'mapstruct', version: versions.mapstruct
  implementation group: 'org.postgresql', name: 'postgresql', version: versions.postgresql

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '3.1.7'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-openfeign-core', version: '3.1.7'
  implementation group: 'io.github.openfeign', name: 'feign-core', version: '12.1'
  implementation group: 'io.github.openfeign', name: 'feign-jackson', version: '12.1'

  implementation group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '4.0.0'
  implementation group: 'uk.gov.hmcts.reform', name: 'idam-client', version: '2.0.0'
  implementation group: 'io.springfox', name: 'springfox-boot-starter', version: versions.springfoxSwagger

  testImplementation group: 'com.github.hmcts', name: 'ccd-test-definitions', version: '7.0.0'
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: versions.serviceAuthVersion
  implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '2.0.1'

  testImplementation(platform('org.junit:junit-bom:5.9.2'))
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  testImplementation group: 'org.springframework.security', name: 'spring-security-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  implementation group: 'io.cucumber', name: 'cucumber-java', version: '7.11.1'

  implementation (group: 'org.glassfish.web', name: 'el-impl', version: '2.2.1-b05') {
    exclude group: 'javax.el', module: 'el-api'
  }
  implementation group: 'org.glassfish', name: 'jakarta.el', version: '4.0.2'
  implementation group: 'javax.el', name: 'javax.el-api', version: '3.0.1-b06'
  implementation group: 'org.testcontainers', name: 'junit-jupiter', version: testcontainersVersion

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath
  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath
  cftlibImplementation group: 'org.springframework.boot', name: 'spring-boot-devtools'
  integrationTestImplementation group: 'com.github.tomakehurst', name: 'wiremock-jre8', version: '2.25.1'
  integrationTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '2.2.4.RELEASE'
  integrationTestImplementation group: 'org.testcontainers', name: 'postgresql', version: versions.testcontainers
  integrationTestImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  testImplementation group: 'org.mockito', name: 'mockito-inline', version: '3.11.1'

  testImplementation group: 'io.cucumber', name: 'cucumber-junit', version: '7.11.1'
  testImplementation group: 'junit', name: 'junit', version: '4.13.2'
  testImplementation group: 'net.javacrumbs.json-unit', name: 'json-unit-assertj', version: '2.36.0'
  testImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.17.6'
  testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-contract-stub-runner', version: '3.1.0'
  testImplementation group: 'org.mockito', name: 'mockito-inline', version: '3.11.1'
  testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.9'
  testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version: '2.0.9'
  testImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
}

mainClassName = 'uk.gov.hmcts.reform.jps.Application'

bootJar {
  archiveFileName = "jps-judicial-payment-service.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

bootWithCCD {
  group("cftlib")
  doFirst {
    // Set this flag to 'true', if you want the CFTLib containers to be recreates each time
    environment "RSE_LIB_CLEAN_BOOT", false
  }

  authMode = uk.gov.hmcts.rse.AuthMode.Local
  // TODO: update when we have db connections and authorised services
  environment("DATA_STORE_S2S_AUTHORISED_SERVICES", "ccd_gw,ccd_gateway,ccd_data,ccd_next_hearing_date_updater")
  environment("RSE_LIB_S2S_PORT", 4502)
  environment("RSE_LIB_ADDITIONAL_DATABASES", "jps")
  environment("JUDICIAL_PAYMENT_SERVICE_DB_HOST", "localhost")
  environment("JUDICIAL_PAYMENT_SERVICE_DB_USERNAME", "postgres")
  environment("JUDICIAL_PAYMENT_SERVICE_DB_PASSWORD", "postgres")
  environment("JUDICIAL_PAYMENT_SERVICE_DB_PORT", "6432")
  environment("JUDICIAL_PAYMENT_SERVICE_DB_NAME", "jps")
  environment("JUDICIAL_PAYMENT_SERVICE_DB_OPTIONS", "?ssl=disable")
  environment("S2S_URL", "http://localhost:8489")
}

void loadEnvFile(Task execTask, String envFilePath) {
  if (file(envFilePath).exists()) {
    logger.quiet("Loading environment variables from '${envFilePath}'")

    file(envFilePath).readLines().each() {
      if (it.startsWith('# ')) {
        if (it.startsWith('# WARNING')) {
          // copy warning to console
          logger.quiet(it)
        }
      } else if (!it.isEmpty()) {
        def (key, value) = it.tokenize('=')
        execTask.environment(key, value)
      }
    }
  } else {
    logger.quiet("Skipping load of environment variables from '${envFilePath}' as file not found.")
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

flyway {
  url = System.getenv('FLYWAY_URL')
  user = System.getenv('FLYWAY_USER')
  password = System.getenv('FLYWAY_PASSWORD')
  baselineOnMigrate = true
}

task migratePostgresDatabase(type: FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
}
